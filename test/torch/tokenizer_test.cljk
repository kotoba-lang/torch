(ns torch.tokenizer-test
  (:require [clojure.test :refer [deftest is]]
            [torch.tokenizer :as tokenizer]))

(defn- byte-token [n]
  (str "<0x"
       #?(:clj (format "%02X" n)
          :cljs (.toUpperCase (.padStart (.toString n 16) 2 "0")))
       ">"))

(def byte-tokens (mapv byte-token (range 256)))

(def fixture
  (tokenizer/tokenizer
   {:tokens (vec (concat ["<unk>" "<bos>" "<eos>"
                          "h" "e" "l" "o" "he" "ll" "hell" "hello" "▁"]
                        byte-tokens))
    :merges [["h" "e"] ["l" "l"] ["he" "ll"] ["hell" "o"]]
    :unk-id 0 :bos-id 1 :eos-id 2 :add-bos? true :space-prefix "▁"}))

(deftest bpe-merges-and-special-tokens
  (is (= [1 10] (tokenizer/encode fixture "hello")))
  (is (= "hello" (tokenizer/decode fixture [1 10 2])))
  (is (= " hello" (tokenizer/decode fixture (tokenizer/encode fixture " hello")))))

(deftest unicode-round-trips-through-byte-fallback
  (let [text "猫🙂"
        ids (tokenizer/encode fixture text)]
    (is (> (count ids) 2))
    (is (= text (tokenizer/decode fixture ids)))
    (let [{s1 :state t1 :text} (tokenizer/decode-step fixture (second ids))
          {s2 :state t2 :text} (tokenizer/decode-step fixture s1 (nth ids 2))]
      (is (map? s2))
      (is (string? t1))
      (is (string? t2)))))

(deftest missing-byte-fallback-is-explicit
  (let [small (tokenizer/tokenizer {:tokens ["<unk>" "a"] :merges [] :unk-id 0})]
    (is (= [0] (tokenizer/encode small "猫")))))

(deftest sentencepiece-selects-the-highest-scoring-complete-path
  (let [sp (tokenizer/tokenizer
            {:tokens ["<unk>" "<s>" "</s>" "▁" "h" "i" "▁h" "hi" "▁hi"]
             :scores [0 0 0 -3 -2 -2 -1 -1 2]
             :model :sentencepiece :space-prefix "▁"
             :unk-id 0 :bos-id 1 :eos-id 2 :add-bos? true})]
    (is (= [1 8] (tokenizer/encode sp "hi")))
    (is (= [1 8 8] (tokenizer/encode sp "hi hi")))
    (is (= " hi hi" (tokenizer/decode sp [1 8 8])))))
