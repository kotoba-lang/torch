(ns torch.metal-bundle-export-test
  (:require [clojure.edn :as edn]
            [clojure.test :refer [deftest is]]
            [num.array :as arr]
            [num.cpu :as cpu]
            [torch.metal-bundle-export :as export])
  (:import [java.nio ByteBuffer ByteOrder]
           [java.nio.charset StandardCharsets]
           [java.nio.file Files]
           [java.nio.file.attribute FileAttribute]))

(deftest atomically-streams-manifest-and-dense-payload
  (let [backend (cpu/cpu-backend)
        path (Files/createTempFile "torch-metal-export-" ".tgb"
                                   (make-array FileAttribute 0))
        weights [{:w (arr/from-vec backend [1.25 -2.5 3.75 4.0] [2 2])}]]
    (try
      (let [manifest (export/export!
                      path {:format :torch/llama-metal-bundle-v3
                            :config {:vocab 2}} weights)
            bytes (Files/readAllBytes path)
            view (doto (ByteBuffer/wrap bytes) (.order ByteOrder/LITTLE_ENDIAN))
            magic-bytes (byte-array 8)
            _ (.get view magic-bytes)
            manifest-length (.getInt view)
            manifest-bytes (byte-array manifest-length)
            _ (.get view manifest-bytes)
            parsed (edn/read-string
                    (String. manifest-bytes StandardCharsets/UTF_8))
            payload (get-in parsed [:weights 0 :w :payload])]
        (is (= "TGBNDL1\n" (String. magic-bytes StandardCharsets/US_ASCII)))
        (is (= manifest parsed))
        (is (= {:offset 0 :length 16} payload))
        (is (= [1.25 -2.5 3.75 4.0]
               (mapv (fn [_] (double (.getFloat view))) (range 4))))
        (is (= (+ 12 manifest-length 16) (alength bytes))))
      (finally (Files/deleteIfExists path)))))
