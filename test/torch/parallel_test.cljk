(ns torch.parallel-test
  (:require [clojure.test :refer [deftest is]]
            [torch.parallel :as parallel]))

(deftest tensor-parallel-projections-equal-unsharded-linear
  (let [x [1.0 2.0 3.0 4.0]
        weights [[1.0 0.0 0.0 1.0]
                 [0.0 1.0 1.0 0.0]
                 [1.0 1.0 1.0 1.0]]]
    (is (= (parallel/linear x weights)
           (parallel/column-parallel x (parallel/column-shards weights 2))))
    (is (= (parallel/linear x weights)
           (parallel/row-parallel x (parallel/row-shards weights 2))))))

(deftest multi-node-topology-and-pipeline-wave-are-deterministic
  (let [topology (parallel/topology
                  {:nodes ["b70-a" "b70-b" "gad-a" "gad-b"]
                   :tensor-parallel 2 :pipeline-parallel 2})]
    (is (= {:rank 3 :node "gad-b" :pipeline-rank 1 :tensor-rank 1}
           (parallel/rank-spec topology 3)))
    (is (= [[0 4] [0 4] [4 8] [4 8]]
           (mapv :layers (parallel/pipeline-plan 8 topology))))
    (is (= 6 (count (parallel/pipeline-events 2 3))))
    (is (= :host-staged
           (:collectives
            (parallel/topology-summary topology
              [{:collective :host-staged} {:collective :device-local}]))))))

(deftest transport-preserves-versioned-activation-envelope
  (let [mailbox (atom {})
        transport (parallel/fn-transport
                   #(swap! mailbox assoc %1 %2)
                   #(get @mailbox %))
        envelope (parallel/activation-envelope 0 1 7 [1.0 2.0])]
    (parallel/send-rank! transport 1 envelope)
    (is (= envelope (parallel/receive-rank! transport 1)))))
