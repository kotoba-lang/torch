(ns torch.speculative-test
  (:require [clojure.test :refer [deftest is]]
            #?(:clj [num.array :as arr])
            [torch.speculative :as speculative]))

(deftest greedy-verification-accepts-prefix-and-target-correction
  (is (= {:tokens [1 2 0] :drafted 3 :accepted 2 :all-accepted? false}
         (speculative/verify-greedy
          [1 2 2] [[0.0 2.0 1.0] [0.0 1.0 2.0] [3.0 1.0 2.0]]))))

(deftest mtp-heads-produce-draft-and-aggregate-metrics
  (is (= [1 0 2]
         (speculative/mtp-draft [[0.0 2.0] [3.0 0.0] [0.0 1.0 4.0]])))
  (is (= {:steps 2 :drafted 5 :accepted 4 :acceptance-rate 0.8}
         (speculative/metrics [{:drafted 3 :accepted 2}
                               {:drafted 2 :accepted 2}])))
  (is (= [1 2]
         (speculative/mtp-draft-candidates
          [[[1 3.0] [0 2.0]] [[2 4.0] [3 1.0]]]
          {:temperature 0.0})))
  (is (= {:tokens [1 2 0] :drafted 3 :accepted 2 :all-accepted? false}
         (speculative/verify-greedy-tokens [1 2 2] [1 2 0]))))

(deftest stochastic-rejection-samples-positive-residual
  (let [result (speculative/verify-stochastic
                [0] [[0.9 0.1]] [[0.1 0.9]] [0.5 0.2])]
    (is (= [1] (:tokens result)))
    (is (= 0 (:accepted result)))
    (is (false? (:all-accepted? result)))))

#?(:clj
   (deftest mtp-step-connects-head-target-device-verification-and-continuous-result
     (let [calls (atom [])
           draft-logits {:shape [4 8] :kind :draft}
           target-logits {:shape [4 8] :kind :target}
           next-logits {:shape [1 8] :kind :next}
           runtimes [{:runtime :committed}]
           step (speculative/make-mtp-step-fn
                 {:draft-token-count 4
                  :verify-options {:temperature 0.7}
                  :draft-fn
                  (fn [_request _runtimes request-id limit]
                    (swap! calls conj [:draft request-id limit])
                    {:tokens [1 2 3 4] :logits draft-logits})
                  :target-fn
                  (fn [_request _runtimes request-id tokens]
                    (swap! calls conj [:target request-id tokens])
                    {:logits target-logits :next-logits next-logits
                     :runtimes runtimes})})]
       (with-redefs [arr/speculative-rejection-rows
                     (fn [target draft tokens options]
                       (swap! calls conj [:verify target draft tokens options])
                       {:tokens [1 2 7] :drafted 4 :accepted 2
                        :all-accepted? false})]
         (is (= {:tokens [1 2 7] :drafted 4 :accepted 2
                 :all-accepted? false
                 :logits next-logits :runtimes runtimes}
                (step {:id :request} [{:runtime :old}] :request-1 4)))
         (is (= [[:draft :request-1 4]
                 [:target :request-1 [1 2 3 4]]
                 [:verify target-logits draft-logits [1 2 3 4]
                  {:temperature 0.7}]]
                @calls))))))

#?(:clj
   (deftest mtp-step-bounds-the-draft-head-by-remaining-generation
     (let [seen-limit (atom nil)
           step (speculative/make-mtp-step-fn
                 {:draft-token-count 4
                  :draft-fn (fn [_ _ _ limit]
                              (reset! seen-limit limit)
                              {:tokens [1] :logits :draft})
                  :target-fn (fn [_ _ _ _]
                               {:logits :target :next-logits :next
                                :runtimes []})})]
       (with-redefs [arr/speculative-rejection-rows
                     (fn [& _] {:tokens [1] :drafted 1 :accepted 1
                                :all-accepted? true})]
         (is (= 1 (:accepted (step {} [] :request-2 1))))
         (is (= 1 @seen-limit))))))
